# gnuplot command file

# parameterised startStop command file
# invoke using -e option to set parameters, eg:
# gnuplot -e "lang='de'" onramp_IDM.gpl

#set datafile separator ","

# parameters
if (!exists("inputPath")) inputPath = "./"
if (!exists("outputPath")) outputPath = "./"
if (!exists("ldm")) ldm = "IDM"
if (!exists("lang")) lang = "en"
#print "LDM=".ldm
#print "lang=".lang

# variables
baseFile = "onramp_".ldm
vehicle = ldm."1"

# input files for road id=0
fundFile = inputPath.baseFile.".fund_".vehicle.".csv"
stFile = inputPath.baseFile.".st.route_main.csv"
detFile(n) = sprintf(inputPath.baseFile.".det.road_1.x_%d.csv",n)
detFile2(n) = sprintf(inputPath.baseFile.".det.road_2.x_%d.csv",n)
roadSourceFile = inputPath.baseFile.".source.road_1.csv"
rampSourceFile = inputPath.baseFile.".source.road_1.csv"
trajectoriesFile = inputPath.baseFile.".traj.route_main.csv"

# output files
fundOutput = outputPath.baseFile.".fund_".vehicle.".eps"
veqOutput = outputPath.baseFile.".veq_".vehicle.".eps"
vqOutput = outputPath.baseFile.".vq_".vehicle.".eps"
detOutput = outputPath.baseFile.".det_q.eps"
detmicroOutput = outputPath.baseFile.".det_q_micro.eps"
inflowOutput = outputPath.baseFile.".inflow_log.eps"
v2dOutput = outputPath.baseFile.".v2d.eps"
v2dallOutput = outputPath.baseFile.".v2d_all.eps"
a2dallOutput = outputPath.baseFile.".a2d_all.eps"
trajectoriesOutput = outputPath.baseFile.".trajectories.eps"

print "fundOutput = ",fundOutput
print "please make sure that ".outputPath." already exists"


########  Languages  #####################################

# English
fundTitle =  "Fundamental diagram Q({/Symbol r})"
veqTitle = "Equilibrium velocity V_{eq}({/Symbol r})"
vqTitle = "V-Q equilibrium relation V_{eq}(Q)"
densityLabel = "Density {/Symbol r} (vehicles/km)"
flowLabel = "Flow Q (vehicles/h)"
veqLabel = "Velocity V_{eq} (km/h)"
timeLabel = "Time (s)"
timeMinuteLabel = "Time (min)"
distanceLabel = "Distance (m)"
velocityLabel = "Velocity (km/h)"
accelerationLabel = "Acceleration (m/s^2)"
positionLabel = "Position (km)"
entranceDistanceLabel = "Distance from entrance (km)"
enteredVehicleLabel = "Number of entered vehicles"
inflowRoadEnteredTitle = "upstream Boundary"
inflowRampEnteredTitle = "onramp Boundary"
inflowRoadFlowTitle = "demand Q(t) in veh/h"
colorboxLabel = "V (km/h)"

# Deutsch
if (lang eq "de")\
  fundTitle =  "Fundamentaldiagram Q({/Symbol r})";\
  veqTitle = "Geschwindigkeit V_{eq} (km/h)";\
  vqTitle = "Gleichgewichts-Geschwindigkeit V_{eq}({/Symbol r})";\
  densityLabel = "Dichte {/Symbol r} (1/km)";\
  flowLabel = "Fluss Q (1/h)";\
  veqLabel = "Geschwindigkeit V_{eq} (km/h)";\
  timeLabel = "Zeit (s)";\
  timeMinuteLabel = "Zeit (min)";\
  distanceLabel = "Abstand (m)";\
  velocityLabel = "Geschwindigkeit (km/h)";\
  accelerationLabel = "Beschleunigung (m/s^2)";\
  positionLabel = "Ort (km)";\
  entranceDistanceLabel = "Abstand von Zufahrt (km)";\
  enteredVehicleLabel = "Anzahl eingefahrender Fahrzeuge";\
  inflowRoadEnteredTitle = "Zufluss Hauptfahrbahn";\
  inflowRampEnteredTitle = "Zufluss Auffahrt";\
  inflowRoadFlowTitle = "Nachfrage Q(t) in Fz/h"\
  colorboxLabel = "V (km/h)"


########  Line Styles  ###################################

set style line 1 linetype 1 linewidth 7 pointtype 7 pointsize 1.5 lc rgb "#000000"
set style line 2 linetype 7 linewidth 3 pointtype 5 pointsize 1.5 lc rgb "#dd0000"
set style line 3 linetype 1 linewidth 7 pointtype 9 pointsize 1.5 lc rgb "#ff8800"
set style line 4 linetype 3 linewidth 3 pointtype 11 pointsize 1.5 lc rgb "#00aa44"
set style line 5 linetype 5 linewidth 7 pointtype 13 pointsize 1.5 lc rgb "#220099"

min(x,y)=(x<y) ? x : y

set term post eps enhanced color dash "Helvetica" 26
set nogrid


########  Plot Fundamental Diagrams  #####################

set xlabel densityLabel

set title ldm." ".fundTitle
set ylabel flowLabel
set out fundOutput
plot fundFile using 1:4 title "" with lines linestyle 10

set title ldm." ".veqTitle
set ylabel veqLabel
set out veqOutput
plot fundFile using 1:3 title "" with lines linestyle 10

set title ldm." ".vqTitle
set xlabel flowLabel
set out vqOutput
plot fundFile u 4:3 title "" with lines linestyle 10

########################################################

set size 1,1
set title ""
set key box

########################################################

set xlabel densityLabel
set ylabel flowLabel

set xrange [0:150]
set out detOutput
plot\
 detFile(8000)  using ($5/$4):5 title "-7 km" with points linestyle 6,\
 detFile(10000) using ($5/$4):5 title "-5 km" with points linestyle 4,\
 detFile(12000) using ($5/$4):5 title "-3 km" with points linestyle 2,\
 detFile(14000) using ($5/$4):5 title "-1 km" with points linestyle 3,\
 fundFile using 1:4 title "FD" with lines linestyle 10

#set out detmicroOutput
#plot\
# detFile(8000)  using ($9*3600/$8):($9*3600) title "-7 km" with points linestyle 6,\
# detFile(10000) using ($9*3600/$8):($9*3600) title "-5 km" with points linestyle 4,\
# detFile(12000) using ($9*3600/$8):($9*3600) title "-3 km" with points linestyle 2,\
# detFile(14000) using ($9*3600/$8):($9*3600) title "-1 km" with points linestyle 1,\
# fundFile using 1:4 title "FD" with lines linestyle 10

######################################
# logging of inflows for testing
######################################

set auto x
set xlabel timeMinuteLabel
set ylabel enteredVehicleLabel
set out inflowOutput
plot\
 roadSourceFile using ($1/60.0):6 title inflowRoadEnteredTitle with lines linestyle 1,\
 rampSourceFile using ($1/60.0):6 title inflowRampEnteredTitle with lines linestyle 3,\
 roadSourceFile using ($1/60.0):5 title inflowRoadFlowTitle with lines linestyle 2

############### 3d ###########

set palette defined (0 "red", 20 "orange", 40 "yellow", 60 "green", 80 "blue",  100 "#dd00ff" )
set cbrange [0:140]
set pm3d; set pm3d map
#set contour surface
set cntrparam bspline
set cntrparam levels 15
unset clabel  # dann lauter gleiche Kontourlinien;
              # Farbe und Typ mit "w l ls" beim splot-Kommando

#set view 20,63
#set hidden3d
set ticslevel 0
set nogrid

##################################

set nokey

set size 1.3,1.2
set label 1 colorboxLabel at screen 1.1,1.02

xmin=-8  # km
xmax=2
xramp=15.  # km
tmin=0
tmax=90 # min
tmax=120
#tshift=20.

set term post eps enhanced color solid "Helvetica" 30

set xlabel timeMinuteLabel
set xrange [tmin:tmax]
set xtics 30

set ylabel entranceDistanceLabel offset -1.5,0
set yrange [xmin:xmax]

unset surface
set out v2dOutput
splot stFile using ($1/60.0):($2/1000.0-xramp):($3*3.6) with lines linestyle 99

####

set auto x
set auto y
set ylabel positionLabel offset -1.0,0
set out v2dallOutput
splot stFile using ($1/60.0):($2/1000.0):($3*3.6) with lines linestyle 99

set label 1 "a (m/s^2)" at screen 1.1,1.02
set palette defined (-2 "red", -0.5 "orange", 0 "yellow", 0.2 "green", 0.6 "blue",  1.0 "#dd00ff", 1.5 "black" )
set cbrange [-2:2]
set out a2dallOutput
splot stFile using ($1/60.0):($2/1000.0):4 with lines linestyle 99

set nolabel
unset colorbox
unset pm3d

set auto x
set xrange [0:7]
set xtics 1
set auto y
set out trajectoriesOutput
plot trajectoriesFile using ($1/60.0):($4):(int($2) % 5 + 1) title "" with points lc variable
