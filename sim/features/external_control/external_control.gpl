# gnuplot command file

# parameterised command file
# invoke using -e option to set parameters, eg:
# gnuplot -e "ldm='IDM'" externalControl.gpl

set datafile separator ","

# set the output type
#set terminal postscript eps enhanced color solid font "Helvetica" 26
set term png size 900,600

outputEnding=".png"
print "outputEnding=".outputEnding

# parameters
if (!exists("inputPath")) inputPath = "./"
if (!exists("outputPath")) outputPath = "./figs/"
if (!exists("ldm")) ldm = "IDM"
print "LDM=".ldm

print "scenario=".scenario

route = "route_main"
print "route=".route

inputEnding=".csv"
print "inputEnding=".inputEnding

# variables
baseFile =  scenario."_".ldm
print "baseFile=".baseFile

print "please make sure that output directory ".outputPath." already exists"

set nogrid

########  Plot Fundamental Diagram  #####################

set xlabel "Density (vehicles/km)"
set ylabel  "Flow (vehicles/h)"
fundFile = inputPath.baseFile.".fund_".ldm."1.csv"
fundOutput = outputPath.baseFile.".fund_".ldm."1".outputEnding
set out fundOutput
print "plot = ".fundOutput
plot fundFile using 1:4 title "" with lines lw 3

########  Plot Floating Cars  ############################


set xlabel "Time (s)"

set ylabel "Speed (km/h)"
fcOutput = outputPath.baseFile.".cars_v.".route.outputEnding
set out fcOutput
print "plot = ".fcOutput
plot\
   baseFile.".car.".route.".000011".inputEnding u 1:(3.6*$6) t "External lead car" w l lw 4,\
   baseFile.".car.".route.".000010".inputEnding u 1:(3.6*$6) t "Following car 1" w l lw 2,\
   baseFile.".car.".route.".000008".inputEnding u 1:(3.6*$6) t "Car 2" w l lw 2,\
   baseFile.".car.".route.".000006".inputEnding u 1:(3.6*$6) t "Car 4" w l lw 2,\
   baseFile.".car.".route.".000004".inputEnding u 1:(3.6*$6) t "Car 6" w l lw 2,\
   baseFile.".car.".route.".000002".inputEnding u 1:(3.6*$6) t "Car 8" w l lw 2,\
   baseFile.".car.".route.".000001".inputEnding u 1:(3.6*$6) t "Car 10" w l lw 2
   
set ylabel "Speed (km/h)"
fcOutput = outputPath.baseFile.".cars_acc.".route.outputEnding
set out fcOutput
print "plot = ".fcOutput
plot\
   baseFile.".car.".route.".000010".inputEnding u 1:7 t "Following car 1" w l lw 2,\
   baseFile.".car.".route.".000008".inputEnding u 1:7 t "Car 2" w l lw 2,\
   baseFile.".car.".route.".000006".inputEnding u 1:7 t "Car 4" w l lw 2,\
   baseFile.".car.".route.".000004".inputEnding u 1:7 t "Car 6" w l lw 2,\
   baseFile.".car.".route.".000002".inputEnding u 1:7 t "Car 8" w l lw 2,\
   baseFile.".car.".route.".000001".inputEnding u 1:7 t "Car 10" w l lw 2

##########################################################

set xrange [0:360]
set xlabel "Time (s)"
set ylabel "Space (m)"
trajOutput = outputPath.baseFile.".traj.".route.outputEnding
set out trajOutput
print "plot = ".trajOutput
plot baseFile.".traj.".route."".inputEnding u 1:3:(int($9) % 5 + 1) title "" with points lt 7 ps 0.3 lc variable
   
exit
