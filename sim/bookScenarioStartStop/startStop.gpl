# gnuplot command file

# parameterised startStop command file
# invoke using -e option to set parameters, eg:
# gnuplot -e "ldm='IDM'" startStop.gpl

set datafile separator ","

# set the output type
set terminal postscript eps enhanced color solid font "Helvetica" 26

# parameters
if (!exists("inputPath")) inputPath = "./"
if (!exists("outputPath")) outputPath = "./"
if (!exists("ldm")) ldm = "IDM"
if (!exists("lang")) lang = "en"
#print "LDM=".ldm
#print "lang=".lang

#  startStop_IDM.car.origin_1.000021.csv
# variables
baseFile = "startStop_".ldm
vehicle = ldm."1"
c0 = 27

# input files
fundFile = inputPath.baseFile.".fund_".vehicle.".csv"
if (ldm eq "IDM_w_noise") fundFile = inputPath.baseFile.".fund_IDM1.csv"
carFile(n) = sprintf(inputPath.baseFile.".car.route_main.%06d.csv",c0-n)
carLsaFile(n) = sprintf(inputPath.baseFile."LSA.car%d.csv",c0+n)
trajectoriesFile = inputPath.baseFile.".traj.route_main.csv"

# output files
fundOutput = outputPath.baseFile.".fund_".vehicle.".eps"
veqOutput = outputPath.baseFile.".veq_".vehicle.".eps"
vqOutput = outputPath.baseFile.".vq_".vehicle.".eps"
distanceOutput = outputPath.baseFile.".s.eps"
velocityOutput = outputPath.baseFile.".v.eps"
accelerationOutput = outputPath.baseFile.".a.eps"
fuelOutput = outputPath.baseFile.".fuel.eps"
fuelOutput100 = outputPath.baseFile.".fuel100.eps"
trajectoriesOutput = outputPath.baseFile.".trajectories.eps"

print "fundOutput = ",fundOutput
print "please make sure that output directory ".outputPath." already exists"



########  Languages  #####################################

# English
fundTitle =  "Fundamental diagram Q({/Symbol r})"
veqTitle = "Equilibrium velocity V_{eq}({/Symbol r})"
vqTitle = "V-Q equilibrium relation V_{eq}(Q)"
densityLabel = "Density {/Symbol r} (vehicles/km)"
flowLabel = "Flow Q (vehicles/h)"
veqLabel = "Velocity V_{eq} (km/h)"
timeLabel = "Time (s)"
distanceLabel = "Distance (m)"
velocityLabel = "Speed (km/h)"
accelerationLabel = "Acceleration (m/s^2)"
fuelLabel = "Consumption rate (l/h)"
fuelLabel100 = "Consumption (l/100km)"
car(n) = sprintf("Car %d",n)

# Deutsch
if (lang eq "de") fundTitle =  "Fundamentaldiagram Q({/Symbol r})";\
  veqTitle = "Geschwindigkeit V_{eq} (km/h)";\
  vqTitle = "Gleichgewichts-Geschwindigkeit V_{eq}({/Symbol r})";\
  densityLabel = "Dichte {/Symbol r} (1/km)";\
  flowLabel = "Fluss Q (1/h)";\
  veqLabel = "Geschwindigkeit V_{eq} (km/h)";\
  timeLabel = "Zeit (s)";\
  distanceLabel = "Abstand (m)";\
  velocityLabel = "Geschwindigkeit (km/h)";\
  accelerationLabel = "Beschleunigung (m/s^2)";\
  car(n) = sprintf("Kfz %d",n)


########  Line Styles  ###################################

set style line 1 linetype 1 linewidth 4 pointtype 7 pointsize 1.5
set style line 2 linetype 2 linewidth 4 pointtype 5 pointsize 1.5
set style line 3 linetype 3 linewidth 4 pointtype 9 pointsize 1.5
set style line 4 linetype 4 linewidth 4 pointtype 11 pointsize 1.5
set style line 5 linetype 7 linewidth 4 pointtype 13 pointsize 1.5

set style line 10 linetype 1 linewidth 7 pointtype 7 pointsize 1.5
set style line 11 linetype 1 linewidth 6 pointtype 7 pointsize 1.5
set style line 12 linetype 7 linewidth 3 pointtype 5 pointsize 1.5
set style line 13 linetype 3 linewidth 6 pointtype 7 pointsize 1.5

set nogrid

########  Plot Fundamental Diagrams  #####################

set xlabel densityLabel

set title ldm." ".fundTitle
set ylabel flowLabel
set out fundOutput
plot fundFile using 1:4 title "" with lines linestyle 10

set title ldm." ".veqTitle
set ylabel veqLabel
set out veqOutput
plot fundFile using 1:3 title "" with lines linestyle 10

set title ldm." ".vqTitle
set xlabel flowLabel
set out vqOutput
plot fundFile u 4:3 title "" with lines linestyle 10

##########################################################

# common setup for plots
set title ldm

tmin=0
tmax=158
if (ldm eq "BARL") tmax=120
if (ldm eq "KKW") tmax=150
if (ldm eq "NSM") tmax=120
if (ldm eq "FVDM") tmax=360
if (ldm eq "CCS") tmax=250

set xrange [tmin:tmax]
set xtics 30
set xlabel timeLabel

set size 1,0.75
set key right


########  Distance (separation) Plot with real traffic light ##########

set ylabel distanceLabel
set yrange [0:35]
if (ldm eq "BARL") set yrange [0:55]
if (ldm eq "KKW") set yrange [0:55]
if (ldm eq "NSM") set yrange [0:55]
set ytics 10
# first car is obstacle, shift by 1 for physical count 
print "carFile(1)=", carFile(1)," c0=",c0

set out distanceOutput
plot\
  carFile(1)  using 1:9 title car(1)  with lines linestyle 1,\
  carFile(5)  using 1:9 title car(5)  with lines linestyle 2,\
  carFile(10) using 1:9 title car(10) with lines linestyle 3,\
  carFile(15) using 1:9 title car(15) with lines linestyle 4,\
  carFile(20) using 1:9 title car(20) with lines linestyle 5

########  Velocity Plot  #################################

set ylabel velocityLabel
set yrange [0:55]
set out velocityOutput
plot\
  carFile(1)  using 1:(3.6*$6) title car(1)  with lines linestyle 1,\
  carFile(5)  using 1:(3.6*$6) title car(5)  with lines linestyle 2,\
  carFile(10) using 1:(3.6*$6) title car(10) with lines linestyle 3,\
  carFile(15) using 1:(3.6*$6) title car(15) with lines linestyle 4,\
  carFile(20) using 1:(3.6*$6) title car(20) with lines linestyle 5


########  Acceleration Plot  #############################

set ylabel accelerationLabel
set auto y
set ytics 1
if (ldm eq "BARL") set ytics 5
if (ldm eq "GIPPS") set yrange [-1.5:2]
if (ldm eq "KKW") set ytics 2; set yrange [-14:2]
if (ldm eq "NSM") set ytics 5
if (ldm eq "OVM") set ytics 5
set out accelerationOutput
plot\
  carFile(1)  using 1:7 title car(1)  with lines linestyle 1,\
  carFile(5)  using 1:7 title car(5)  with lines linestyle 2,\
  carFile(10) using 1:7 title car(10) with lines linestyle 3,\
  carFile(15) using 1:7 title car(15) with lines linestyle 4,\
  carFile(20) using 1:7 title car(20) with lines linestyle 5


########  Trajectories Plot  #############################

set ylabel distanceLabel
set nolabel
unset colorbox
unset pm3d
set auto x
set auto y
set xrange [0:tmax]
set xtics auto
set ytics auto
set out trajectoriesOutput
plot trajectoriesFile using 1:3:(int($9) % 5 + 1) title "" with points lt 7 ps 0.3 lc variable
