# gnuplot command file

# parameterised fundamental diagrams command file
# invoke using -e option to set parameters, eg:
# gnuplot -e "ldm='IDM'" fund_diagrams.gpl

set datafile separator ","

# set the output type
set terminal postscript eps enhanced color solid font "Helvetica" 26

# parameters
if (!exists("inputPath")) inputPath = "./"
if (!exists("outputPath")) outputPath = "./"
if (!exists("ldm")) ldm = "IDM"
if (!exists("lang")) lang = "en"

# variables
baseFile = "fund_diagrams"

# input files
fundFile = inputPath.baseFile.".fund_".ldm.".csv"
if (ldm eq "IDM_w_noise") fundFile = inputPath.baseFile.".fund_IDM1.csv"

# output files
fundOutput = outputPath.baseFile.".fund_".ldm.".eps"
veqOutput = outputPath.baseFile.".veq_".ldm.".eps"
vqOutput = outputPath.baseFile.".vq_".ldm.".eps"


########  Languages  #####################################

# English
fundTitle =  "Fundamental diagram Q({/Symbol r})"
veqTitle = "Equilibrium velocity V_{eq}({/Symbol r})"
vqTitle = "V-Q equilibrium relation V_{eq}(Q)"
densityLabel = "Density {/Symbol r} (vehicles/km)"
flowLabel = "Flow Q (vehicles/h)"
veqLabel = "Velocity V_{eq} (km/h)"

# Deutsch
if (lang eq "de") fundTitle = "Fundamentaldiagram Q({/Symbol r})";\
  veqTitle = "Geschwindigkeit V_{eq} (km/h)";\
  vqTitle = "Gleichgewichts-Geschwindigkeit V_{eq}({/Symbol r})";\
  densityLabel = "Dichte {/Symbol r} (1/km)";\
  flowLabel = "Fluss Q (1/h)";\
  veqLabel = "Geschwindigkeit V_{eq} (km/h)"


########  Line Styles  ###################################

set style line 1 linetype 1 linewidth 4 pointtype 7 pointsize 1.5
set style line 2 linetype 2 linewidth 4 pointtype 5 pointsize 1.5
set style line 3 linetype 3 linewidth 4 pointtype 9 pointsize 1.5
set style line 4 linetype 4 linewidth 4 pointtype 11 pointsize 1.5
set style line 5 linetype 7 linewidth 4 pointtype 13 pointsize 1.5

set style line 10 linetype 1 linewidth 7 pointtype 7 pointsize 1.5

set style line 11 linetype 1 linewidth 6 pointtype 7 pointsize 1.5
set style line 12 linetype 7 linewidth 3 pointtype 5 pointsize 1.5
set style line 13 linetype 3 linewidth 6 pointtype 7 pointsize 1.5

set nogrid


#######  Scaling  ########################################

xscale=1.0
if (ldm eq "KCA") xscale = 0.5
if (ldm eq "BARL") xscale = 7.5
if (ldm eq "NSM") xscale = 7.5
tscale=1.0
vscale=xscale/tscale
rhoscale=1.0/xscale
ascale=xscale/(tscale*tscale)


########  Plot Fundamental Diagrams  #####################

set xlabel densityLabel

set title ldm." ".fundTitle
set ylabel flowLabel
set out fundOutput
plot fundFile using 1:4 title "" with lines linestyle 10

set title ldm." ".veqTitle
set ylabel veqLabel
set out veqOutput
plot fundFile using 1:3 title "" with lines linestyle 10

set title ldm." ".vqTitle
set xlabel flowLabel
set out vqOutput
plot fundFile u 4:3 title "" with lines linestyle 10
