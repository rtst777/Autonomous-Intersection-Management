# movsim.org -- gnuplot command file for example data movsim/consumption/data/ostfalia

# invoke using -e option to set parameters, eg:
# gnuplot -e "var='value'" gnuplotfile.gpl

set datafile separator ","

# set the output type
set terminal postscript eps enhanced color solid font "Helvetica" 26

# parameters
inputPathData = "./data/"
inputPathSim = "./"
outputPath = "./"
#print "please make sure that output directory ".outputPath." already exists"

print "input parameter: baseName=".baseName
print "input parameter: consumptionModelName=".consumptionModelName
print "input parameter: projectName=".projectName

# variables
inputFileEndingData=".dat"  # empiric data (input)
inputFileEndingSim=".csv"   # movsim output
outputFileEnding=".eps"

gpsInput="-GPS"
canInput="-CAN"
modelOutput="-GPS_".consumptionModelName
specCons=".specCons_"

# input files
gpsFile=inputPathData.baseName.gpsInput.inputFileEndingData
canFile=inputPathData.baseName.canInput.inputFileEndingData
modelFile=inputPathSim.baseName.modelOutput.inputFileEndingSim
specConsInput=inputPathSim.projectName.specCons.consumptionModelName.inputFileEndingSim

print "gps input file=".gpsFile
print "empiric CAN data input file=".canFile
print "model output file=".modelFile

# plot output files
speedGpsOutput = outputPath.baseName.".speed".gpsInput.outputFileEnding
heightGpsOutput = outputPath.baseName.".height".gpsInput.outputFileEnding

speedCanOutput = outputPath.baseName.".speed".canInput.outputFileEnding
fuelCanOutput = outputPath.baseName.".fuel".canInput.outputFileEnding

speedCompareOutput = outputPath.baseName.".speed-compare".gpsInput.modelOutput.outputFileEnding
accCompareOutput = outputPath.baseName.".acc-compare".gpsInput.modelOutput.outputFileEnding
fuelModelOutput = outputPath.baseName.".fuel".modelOutput.outputFileEnding

fuelCompareOutput = outputPath.baseName.".fuel-compare".canInput.modelOutput.outputFileEnding

# plot output files
specConsPressureOutput = outputPath.baseName.specCons."pressure_".consumptionModelName.outputFileEnding
specConsPowerOutput = outputPath.baseName.specCons."power_".consumptionModelName.outputFileEnding
########  Line Styles  ###################################

set style line 1 linetype 1 linewidth 2 pointtype 7 pointsize 1
set style line 2 linetype 2 linewidth 2 pointtype 5 pointsize 0.5
set style line 3 linetype 3 linewidth 2 pointtype 9 pointsize 1.5
set style line 4 linetype 4 linewidth 2 pointtype 11 pointsize 1.5
set style line 5 linetype 7 linewidth 2 pointtype 13 pointsize 1.5

set style line 10 linetype 1 linewidth 2 pointtype 7 pointsize 1.5
set style line 11 linetype 1 linewidth 2 pointtype 7 pointsize 1.5
set style line 12 linetype 7 linewidth 2 pointtype 5 pointsize 1.5
set style line 13 linetype 3 linewidth 2 pointtype 7 pointsize 1.5

#set nogrid
set grid
set size 1.2,1
set xzeroaxis lt -1

########  Plot Diagrams  #####################

set title "Route: ".baseName.", model=".consumptionModelName
set xlabel "Time (min)"

set ylabel "Kinematic Quantities"
set out speedGpsOutput
print "plot ".speedGpsOutput
plot\
  gpsFile u ($1/60.):12 t "speed [km/h]" w l ls 1,\
  gpsFile u ($1/60.):($26*10) t "10*acceleration [m/s^2]" w l ls 3

#####
set out speedCompareOutput
print "plot ".speedCompareOutput
plot\
  modelFile u ($2/60.):($6*3.6) t "MODEL speed [km/h]" w p ls 1,\
  gpsFile u ($30/60.):12 t "GPS speed [km/h]" w p ls 2

set out accCompareOutput
print "plot ".accCompareOutput
plot\
  modelFile u ($2/60.):7 t "MODEL acceleration [m/s^2]" w p ls 1,\
  gpsFile u ($30/60.):26 t "GPS acceleration [m/s^2]" w p ls 2


set ylabel "Topographic Quantities"
set out heightGpsOutput
print "plot ".heightGpsOutput
plot\
  gpsFile u ($1/60.):($21) t "avg. Height [m]" w l ls 4,\
  gpsFile u ($1/60.):($19) t "abs. Height [m]" w l ls 1,\
  gpsFile u ($1/60.):($24*10) t "10*grade [\%]" w l ls 3

##################################################################

fuel100(cLiterPerH,vKMH) = (vKMH<5) ? 1.0 : cLiterPerH/3600.*100*1000/(vKMH/3.6)

set title "Route: ".baseName
set xlabel "Time (min)"

set ylabel "CAN Quantities"
set out fuelCanOutput
print "plot ".fuelCanOutput
plot\
  canFile u ($2/60.):($19*10) t "10*acceleration [m/s^2]" w l ls 3,\
  canFile u ($2/60.):($5*10) t "10*consumption [l/h]" w l ls 4,\
  canFile u ($2/60.):(fuel100($5,$18)) t "consumption [l/100km]" w l ls 5


set ylabel "CAN Quantities"
set out speedCanOutput
print "plot ".speedCanOutput
plot\
  canFile u ($2/60.):18 t "speed [km/h]" w l ls 1,\
  canFile u ($2/60.):($19*10) t "10*acceleration [m/s^2]" w l ls 3


set title "Route: ".baseName.", model=".consumptionModelName
set ylabel "Model Quantities"
set out fuelModelOutput
print "plot ".fuelModelOutput
plot\
  modelFile u ($1/60.):($9*3600*10) t "10*consumption [l/h]" w l ls 4,\
  modelFile u ($1/60.):(fuel100($9*3600,$6*3.6)) t "consumption [l/100km]" w l ls 5,\
  modelFile u ($1/60.):($11*10) t "10*gear" w l ls 6

set key left
set ylabel "Cumulated Consumption (liter)"
set out fuelCompareOutput
print "plot ".fuelCompareOutput
plot\
  canFile u ($2/60.):20 t "CAN data" w l ls 1,\
  modelFile u ($1/60.):10 t "model" w l ls 3


############################################################################
set style line 99 lt 7 lw 1 linecolor rgb "#000000" #black

#################################
# Definition of Contour lines   (line style=line style indicated at splot)
#################################

#unset contour              # no contour lines
#set contour surface
#set cntrparam bspline
unset clabel
unset key
unset grid

#################################
# Definition of used color-coding
#################################

set pm3d; set pm3d map # gnuplot bug; set pm3d  map alone

set palette defined ( 0 "#dd00ff",  5 "#9933ff", 12 "#6666ff", 20 "#00eeee",\
      25 "green", 30 "#aaff00", 40 "yellow", 70 "orange", 100 "red")

set xlabel "Frequency [rpm]"
set xrange [800:6000]

set ylabel "Torque [Nm]"
set yrange [0:230]

set title "Specific consumption(f,pressure) [g/kWh] for model ".consumptionModelName.", ".baseName
set zrange [200:500]
unset ztics

#set cntrparam levels discrete 200,210,220,230,240,250,260,270,280,290,300 # freely set lines

const=300
set out specConsPressureOutput
splot\
 specConsInput u 1:4:5  w pm3d,\
 canFile u 3:4:(300) t "CAN data" w p palette

## TODO l/h --> g/kWh

#########################################

powerFuncFromM(torque,f)=2*pi*torque*f/60.
print "power=",powerFuncFromM(50,2000)

set title "specific consumption (f,power) [g/kWh] for model ".consumptionModelName.", ".baseName
set ylabel "Power [kW]"
set yrange [0:95]

set out specConsPowerOutput
splot\
 specConsInput u 1:2:5  w pm3d,\
 canFile u 3:(powerFuncFromM($4,$3)/1000.):(300) t "CAN data" w p palette

unset pm3d
reset

